methods {
    getProofOfReserveFeedForAsset(address) returns (address) envfree
    disableProofOfReserveFeed(address)
    enableProofOfReserveFeed(address, address)
    areAllReservesBacked(address[]) returns (bool, bool[]) envfree

    // summarizations:
    latestRoundData() => NONDET
    // Getters: 
    allBacked() returns (bool) envfree
}


// calling function with specific parameters
function call_f_with_params(method f, env e, address asset , address PoRFeed){
    calldataarg args;
    if (f.selector == enableProofOfReserveFeed(address, address).selector){
        enableProofOfReserveFeed(e, asset, PoRFeed);
    } else if (f.selector == disableProofOfReserveFeed(address).selector){
        disableProofOfReserveFeed(e, asset);
    }
    else {
        f(e, args);
    }
}

// modification to the PoR feed of assets
// if enableProofOfReserveFeed called -> price feed must be a non-zero address and 
// if disableProofOfReserveFeed called -> price feed assigned to the asset must be nullified
// if any other function called -> price feed mustn't change
rule PoRFeedChange(address asset, address PoRFeed){
    
    address feedBefore = getProofOfReserveFeedForAsset(asset);
    
    method f; env e;
    call_f_with_params(f, e, asset, PoRFeed);

    address feedAfter = getProofOfReserveFeedForAsset(asset);

    assert f.selector == enableProofOfReserveFeed(address, address).selector => (feedAfter != 0 && feedAfter == PoRFeed);
    assert f.selector == disableProofOfReserveFeed(address).selector => feedAfter == 0;
    assert (f.selector != enableProofOfReserveFeed(address, address).selector && 
            f.selector != disableProofOfReserveFeed(address).selector) => 
                        feedBefore == feedAfter;
}

// if areAllReservesBacked is false then at least one slot in tokenBacked array is false, otherwise all slots in tokenBacked array is true.
rule notAllReservesBacked_UnbackedArry_Correlation(){
    env e; address[] assets;
    
    bool reservesArrayIsBacked = areAllReservesBackedCorrelation(e, assets);
    bool areAllReservesBacked = allBacked();
    assert reservesArrayIsBacked == areAllReservesBacked;
}
